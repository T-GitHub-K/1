INCLUDE	MDOS.H
INCLUDE	STD.H

CODE	SEGMENT
	ASSUME	CS:CODE,DS:CODE,ES:CODE,SS:CODE


	ORG	80H
DTA	DB	80H	DUP(?)
	ORG	100H
	
START:
		MOV	SI,OFFSET DTA[0]
		CMP	BYTE PTR [SI],0
		JNE	NEXT
		JMP	PARA_ERR
NEXT:		INC	SI
		CALL	PARA_SET
		JNC	C_LINE0
		JMP	PARA_ERR
C_LINE0:
		MOV	DI,OFFSET PNAME
	REP	MOVSB
		MOV	AL,NUL
		STOSB

		OPEN_HANDLE	PNAME,0

		JC	OPENERR
		MOV	HANDLE_D,AX

		READ_HANDLE	HANDLE_D,PAL1,384
		
		JC	READERR
		CMP	AX,0
		JE	SETEND
		JMP	SET_PAL
;
PARA_ERR:
		MOV	DX,OFFSET MSGPARA
		JMP	SHORT	MSGOUT

OPENERR:
		MOV	DX,OFFSET MSGSER
		JMP	SHORT	MSGOUT

READERR:
		MOV	DX,OFFSET MSGRER
		JMP	SHORT	MSGOUT

SETEND:
		CLOSE_HANDLE	HANDLE_D

		JC	CLSERR
		MOV	DX,OFFSET MSGEND
		JMP	SHORT	MSGOUT
;
CLSERR:		MOV	DX,OFFSET MSGCER
;

MSGOUT:
		MOV	AH,09H
		INT	21H
EXIT:

		END_PROCESS



SET_PAL:
		CLD
		MOV	DI,OFFSET PAL
		MOV	BX,0
PALA_SET1:
		MOV	AL,PAL1[BX]
		CMP	AL,30H				;∑¨◊∏¿∞∫∞ƒﬁ 0ÇÊÇËè¨Ç≥Ç¢
		JB	PALA_E
		CMP	AL,46H				;∑¨◊∏¿∞∫∞ƒﬁ FÇÊÇËëÂÇ´Ç¢
		JA	PALA_E
		CMP	AL,39H
		JA	PALA_ABC
		SUB	AL,30H
		MOV	P1,AL
		JMP	SHORT PALA_SET2

PALA_E:
		JMP	PARA_ERR

PALA_ABC:
		SUB	AL,37H
		MOV	P1,AL

PALA_SET2:
		MOV	AL,PAL1[BX+1]
		CMP	AL,30H				;∑¨◊∏¿∞∫∞ƒﬁ 0ÇÊÇËè¨Ç≥Ç¢
		JB	PALA_E
		CMP	AL,46H				;∑¨◊∏¿∞∫∞ƒﬁ FÇÊÇËëÂÇ´Ç¢
		JA	PALA_E
		CMP	AL,39H
		JA	PALA_ABC1
		SUB	AL,30H
		MOV	P2,AL
		JMP	SHORT PALA_SET3
PALA_ABC1:
		SUB	AL,37H
		MOV	P2,AL

PALA_SET3:
		XOR	CX,CX
		MOV	AL,P2
		MOV	CL,4
		SHL	AL,CL
		MOV	AH,P1
		XOR	CX,CX
		MOV	CL,4
		SHR	AX,CL
		STOSB				;DI<<AL
		INC	BX
		INC	BX
		CMP	BX,192
		JAE	PALA_SET4
		JMP	PALA_SET1

PALA_SET4:
		MOV	DX,OFFSET PAL
		XOR	BX,BX
		MOV	CX,64
		MOV	AX,1012H
		INT	10H

		JMP	SETEND


PARA_SET	PROC	NEAR
		MOV	CX,0
PARA1:
		LODSB
		CMP	AL,CR
		JE	PARA_END
		CMP	AL,SPC
		JE	PARA1
		CMP	AL,","
		JE	PARA1
		CMP	AL,"/"
		JE	PARA1
		CMP	AL,TAB
		JE	PARA1
		
		PUSH	SI
PARA_CHR:
		INC	CX
		LODSB
		CMP	AL,CR
		JE	CHR_END
		CMP	AL,SPC
		JE	CHR_END
		CMP	AL,","
		JE	CHR_END
		CMP	AL,"/"
		JE	CHR_END
		CMP	AL,TAB
		JE	CHR_END
		JMP	SHORT	PARA_CHR

PARA_END:
		STC
		JMP	SHORT	PARA_EXIT
CHR_END:
		POP	SI
		DEC	SI
		CLC
PARA_EXIT:
		RET
PARA_SET	ENDP


HANDLE_D	DW	?

MSGSER		DB	CR,LF,LF,'••••File Not Found',CR,LF,'$'

MSGRER		DB	CR,LF,LF,'••••Read error',CR,LF,'$'

MSGCER		DB	CR,LF,LF,'••••Cannot Close',CR,LF,'$'

MSGEND		DB	CR,LF,LF,'••••Set PAL DATA',CR,LF,'$'

MSGPARA		DB	CR,LF,LF,"••••Parameter Err",CR,LF,"$"


PNAME	DB	63 DUP(0),0

;		3*64=192Byte

PAL	DB	192	DUP(0)
PAL1	DB	384	DUP(0)

P1	DB	0
P2	DB	0

STACK	DB	100H	DUP(0)

CODE	ENDS
	END	START
