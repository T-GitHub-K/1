;1999/08/06  AM 10:15   Read Date ADD CR+LF Data Change By T.K
;大陸交通燃料データ変換プロ　　228バイト読んで CR+LFコードを挿入230ﾊﾞｲﾄ
;
; 第一パラメータで読込ファイルを指定
; 第二パラメータで書込ファイルを指定する



INCLUDE	MDOS.H
INCLUDE	MACRO.H
INCLUDE	STD.H

;               読み込むデータバイト数
;               書込は必然的に N+2バイトになる
;

BUF_SIZE	EQU	228

CODE	SEGMENT	PUBLIC
	ASSUME	CS:CODE,DS:CODE,SS:CODE,ES:CODE

	ORG	80H
DTA	DB	80H	DUP(?)
	ORG	100H
	
;  		パラメータを取得

START:
		MOV	SI,OFFSET DTA[0]
		CMP	BYTE PTR [SI],0
		JE	MENU_INPUT
		INC	SI
		CALL	PARA_SET
		JNC	C_LINE0
		JMP	PARA_ERR
C_LINE0:
		MOV	DI,OFFSET SOURCE[2]
	REP	MOVSB
		MOV	AL,NUL
		STOSB

		CALL	PARA_SET
		JNC	C_LINE1
		JMP	PARA_ERR
C_LINE1:
		MOV	DI,OFFSET DEST[2]
	REP	MOVSB
		MOV	AL,NUL
		STOSB
		JMP	MAIN
MENU_INPUT:
		DISPLAY		SNAME

		MOV	DX,OFFSET SOURCE
		MOV	AH,0AH
		INT	21H

		MOV	SI,OFFSET SOURCE[2]
		MOV	BL,SOURCE[1]
		XOR	BH,BH
		MOV	BYTE PTR [BX+SI],0

		DISPLAY		DNAME

		MOV	DX,OFFSET DEST
		MOV	AH,0AH
		INT	21H

		MOV	DI,OFFSET DEST[2]
		MOV	BL,DEST[1]
		XOR	BH,BH
		MOV	BYTE PTR [BX+DI],0

;ファイルＯＰＥＮ

MAIN:
		OPEN_HANDLE	SOURCE[2],0

		JC	OPENERR
		MOV	HANDLE_S,AX

		CREATE_HANDLE	DEST[2],0

		JC	MEKEERR
		MOV	HANDLE_D,AX

; ファイル書込

NXTREC:
		INC	COUNT
		
		READ_HANDLE	HANDLE_S,BUFFER,BUF_SIZE
		
		JC	READERR

		CMP	AX,0
		JE	CPYEND
		CMP	COUNT,1
		JE	NXTREC1
		MOV	AXSTK,AX
		MOV	AX,2
		WRITE_HANDLE	HANDLE_D,BUFFER1,AX
		MOV	AX,AXSTK
		
NXTREC1:
		WRITE_HANDLE	HANDLE_D,BUFFER,AX
		
		JC	WRTERR
		CMP	CX,AX
		JE	NXTREC
		JMP	DISK_FULL

OPENERR:
		MOV	DX,OFFSET MSGSER
		JMP	SHORT	MSGOUT

MEKEERR:
		MOV	DX,OFFSET MSGMER
		JMP	SHORT	MSGOUT

READERR:
		MOV	DX,OFFSET MSGRER
		JMP	SHORT	MSGOUT

WRTERR:		MOV	DX,OFFSET MSGWER
		JMP	SHORT	MSGOUT

DELERR:		MOV	DX,OFFSET MSGDER
		JMP	SHORT	MSGOUT

CPYEND:
		CLOSE_HANDLE	HANDLE_D

		JC	CLSERR
		MOV	DX,OFFSET MSGEND
		JMP	SHORT	MSGOUT
;
CLSERR:		MOV	DX,OFFSET MSGCER
;
MSGOUT:
		MOV	AH,09H
		INT	21H
;
		END_PROCESS


DISK_FULL:
		CLOSE_HANDLE	HANDLE_D
		JC	CLSERR
		DELETE_ENTRY	DEST[2]
		JC	DELERR
		MOV	DX,OFFSET MSGFER
		JMP	SHORT	MSGOUT


PARA_ERR:
		MOV	DX,OFFSET MSGPARA
		JMP	SHORT	MSGOUT

PARA_SET	PROC	NEAR
		MOV	CX,0
PARA1:
		LODSB
		CMP	AL,CR
		JE	PARA_END
		CMP	AL,SPC
		JE	PARA1
		CMP	AL,","
		JE	PARA1
		CMP	AL,TAB
		JE	PARA1
		
		PUSH	SI
PARA_CHR:
		INC	CX
		LODSB
		CMP	AL,CR
		JE	CHR_END
		CMP	AL,SPC
		JE	CHR_END
		CMP	AL,","
		JE	CHR_END
		CMP	AL,TAB
		JE	CHR_END
		JMP	SHORT	PARA_CHR

PARA_END:
		STC
		JMP	SHORT	PARA_EXIT
CHR_END:
		POP	SI
		DEC	SI
		CLC
PARA_EXIT:
		RET
PARA_SET	ENDP



MSGDER		DB	CR,LF,LF,"････Delete Err",CR,LF,"$"

MSGFER		DB	CR,LF,LF,"････Disk Full",CR,LF,"$"

MSGSER		DB	CR,LF,LF,'････Source File Not Found',CR,LF,'$'

MSGMER		DB	CR,LF,LF,'････Directory Full',CR,LF,'$'

MSGRER		DB	CR,LF,LF,'････Read error',CR,LF,'$'

MSGWER		DB	CR,LF,LF,'････Disk Full',CR,LF,'$'

MSGCER		DB	CR,LF,LF,'････Cannot Close',CR,LF,'$'

MSGEND		DB	CR,LF,LF,'････Copy Complete',CR,LF,'$'

MSGPARA		DB	CR,LF,LF,"････Parameter Err",CR,LF,"$"

SNAME		DB	CR,LF,'Source name ? $'

DNAME		DB	CR,LF,'Dest name ? $'

SOURCE		DB	62,63 DUP (?)

DEST		DB	62,63 DUP (?)

HANDLE_S	DW	?

HANDLE_D	DW	?

BUFFER		DB	BUF_SIZE DUP(?)

BUFFER1		DB	CR,LF

COUNT		DW	?

AXSTK		DW	?


CODE		ENDS
		END	START
